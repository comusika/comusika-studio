apply plugin: 'base'
apply plugin: 'distribution'
apply plugin: 'com.bmuschko.izpack'

version = '0.1.0-SNAPSHOT'

task wrapper(type: Wrapper, description: 'Creates and deploys the Gradle wrapper to the current directory.') {
    gradleVersion = '3.2'
}

subprojects.each { subproject -> evaluationDependsOn(subproject.path) }

distZip.dependsOn project(':tools').subprojects.assemble
distTar.dependsOn project(':tools').subprojects.assemble

distributions {
    main {
        contents {
            duplicatesStrategy = 'exclude'

            from project.rootDir
            include 'license.txt'
            from 'src/dist'
            include 'readme.txt'

            into('lib') {
                from 'lib'
            }
            project(':modules').subprojects.each { p ->
                p.plugins.withType(JavaPlugin) {
                    into('lib') {
                        from p.jar.archivePath
                        include '*'
                        from p.configurations.runtime
                        include '*'
                    }
                }
            }
            project(':tools').subprojects.each { p ->
                p.plugins.withType(JavaPlugin) {
                    into('lib') {
                        from p.configurations.runtime
                        include '*'
                    }

                    into('bin') {
                        from p.jar.archivePath
                        include '*'
                        rename { filename -> p.name + ".jar" }
                    }
                }
            }
            into('doc') {
                from 'doc'
                include '**'
            }
            into('resources') {
                from 'resources'
                include '**'
            }
            into('bin/lib') {
                from 'lib/native'
                include '**'
            }
        }
    }
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-izpack-plugin:2.1'
    }
}

izpack {
    baseDir = file("$buildDir/distributions")
    installFile = file('src/install-definition.xml')
    outputFile = file("$buildDir/distributions/frinthesia-${version}-installer.jar")
    compression = 'deflate'
    compressionLevel = 9
    appProperties = ['app.group': 'frinthesia', 'app.name': 'frinthesia', 'app.title': 'Frinthesia',
                     'app.version': version, 'app.subpath': "frinthesia-$version"]
}