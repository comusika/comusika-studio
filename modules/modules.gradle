// Shared modules build

version = '0.1.1-SNAPSHOT'
def repositoryName = "comusika-studio"
def githubProfile = 'comusika'
def libraryUrl = 'https://github.com/comusika/comusika-studio'

ext.depsThis = 'Comusika'

ext.depsComusika = [
    githubProfile: githubProfile,
    githubRepo   : repositoryName,
    version      : version
]

ext.depsExbinFrameworkJava = [
    githubProfile: 'exbin',
    githubRepo: 'exbin-framework-java',
    version: '0.2.2-SNAPSHOT'
]

ext.depsExbinAuxiliaryJava = [
    githubProfile: 'exbin',
    githubRepo: 'exbin-auxiliary-java',
    version: '0.2.1-SNAPSHOT'
]

ext.depsXbupJava = [
    githubProfile: 'exbin',
    githubRepo: 'xbup-java',
    version: '0.2.2-SNAPSHOT'
]

ext.depsXbupDataJava = [
    githubProfile: 'exbin',
    githubRepo: 'xbup-data-java',
    version: '0.2.2-SNAPSHOT'
]

ext.depsBinedLibJava = [
    githubProfile: 'exbin',
    githubRepo: 'bined-lib-java',
    version: '0.2.1-SNAPSHOT'
]

['java-library', 'maven-publish'].each {
    apply plugin: it
}

import org.gradle.util.VersionNumber

apply from: '../deps_processing.gradle'

sourceCompatibility = '1.8'

targetCompatibility = '1.8'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    doFirst {
        options.fork = true
    }
}

// Disable tests on build
tasks.withType(Test) {enabled = false}

task testJar(type: Jar, dependsOn: testClasses) {
    archiveClassifier = "tests"
    archiveBaseName = "test-${project.archivesBaseName}"
    from sourceSets.test.output
}

configurations {
    tests
}

artifacts {
    tests testJar
}

task sourceJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.java
}

javadoc {
    if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
        options.addBooleanOption('html5', true)
    }
}

task javadocJar (type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}
